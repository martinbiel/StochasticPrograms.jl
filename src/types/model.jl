"""
    StochasticModel

A mathematical model of a stochastic optimization problem.
"""
struct StochasticModel{N, P <: NTuple{N, StageParameters}}
    parameters::P
    generator::Function
    sp_optimizer::SPOptimizer

    function StochasticModel(generator::Function,
                             parameters::Vararg{StageParameters,N}) where N
        return new{N,typeof(parameters)}(parameters, generator, SPOptimizer(nothing))
    end
end
nstages(::StochasticModel{N}) where N = N
"""
    @stochastic_model(def)

Define a stochastic model capable of instantiating stochastic programs, using the syntax
```julia
sm = @stochastic_model begin
    ...
    @stage x begin
      ...
    end
    ...
end
```
where the inner blocks are [`@stage`](@ref) blocks. At least two stages must be specified in consecutive order. A stochastic model object can later be used to [`instantiate`](@ref) stochastic programs using a given set of scenarios or to create [`SAA`](@ref) models using samplers.

## Examples

The following defines a stochastic model consisitng of the first stage model given by:
```math
  minimize 100xâ‚ + 150xâ‚‚
    s.t  xâ‚ + xâ‚‚ â‰¤ 120
         xâ‚ â‰¥ 40
         xâ‚‚ â‰¥ 20
```
and the second-stage model given by:
```math
  minimize qâ‚(Î¾)yâ‚ + qâ‚‚(Î¾)yâ‚‚
    s.t  6yâ‚ + 10yâ‚‚ â‰¤ 60xâ‚
         8yâ‚ + 5yâ‚‚ â‰¤ 60xâ‚‚
         0 â‰¤ yâ‚ â‰¤ dâ‚(Î¾)
         0 â‰¤ yâ‚‚ â‰¤ dâ‚‚(Î¾)
```
where ``qâ‚(Î¾), qâ‚‚(Î¾), dâ‚(Î¾), dâ‚‚(Î¾)`` depend on the scenario ``Î¾``.

```julia
sm = @stochastic_model begin
    @stage 1 begin
        @variable(model, xâ‚ >= 40)
        @variable(model, xâ‚‚ >= 20)
        @objective(model, Min, 100*xâ‚ + 150*xâ‚‚)
        @constraint(model, xâ‚ + xâ‚‚ <= 120)
    end
    @stage 2 begin
        @decision xâ‚ xâ‚‚
        @uncertain qâ‚ qâ‚‚ dâ‚ dâ‚‚
        @variable(model, 0 <= yâ‚ <= dâ‚)
        @variable(model, 0 <= yâ‚‚ <= dâ‚‚)
        @objective(model, Min, qâ‚*yâ‚ + qâ‚‚*yâ‚‚)
        @constraint(model, 6*yâ‚ + 10*yâ‚‚ <= 60*xâ‚)
        @constraint(model, 8*yâ‚ + 5*yâ‚‚ <= 80*xâ‚‚)
    end
end
```

See also: [`@stage`](@ref), [`@parameters`](@ref), [`@decision`](@ref), [`@uncertain`](@ref)
"""
macro stochastic_model(def)
    stage = 0
    scenariodef = Expr(:block)
    paramdefs = Vector{Expr}()
    def = prewalk(prettify(def)) do x
        x = if @capture(x, @stage n_ arg_)
            if @capture(arg, sp_ = def_)
                x
            else
                stage == 0 && (n == 1 || error("A first stage must be defined."))
                stage == n - 1 || error("Define the stages in coherent order.")
                stage += 1
                push!(paramdefs, :(StageParameters()))
                return :(@stage $n sp = $arg)
            end
        else
            x
        end
        paramdef = if @capture(x, @parameters arg_)
            names = Vector{Symbol}()
            default = Vector{Expr}()
            for paramdef in prettify(arg).args
                if @capture(paramdef, key_Symbol = val_)
                    push!(names, key)
                    push!(default, paramdef)
                elseif @capture(paramdef, key_Symbol)
                    push!(names, key)
                else
                    error("Incorrect @parameters syntax. Specify parameter names, possibly with a default value.")
                end
            end
            :(StageParameters($names; $(default...)))
        elseif @capture(x, @parameters args__)
            args = convert(Vector{Symbol}, args)
            :(StageParameters($args))
        else
            nothing
        end
        if paramdef != nothing
            paramdefs[stage] = paramdef
        end
        scenariodef = if @capture(x, @uncertain var_Symbol::t_Symbol = def_)
            esc(@q begin
                @scenario $t = $def
            end)
        else
            scenariodef
        end
        return x
    end
    stage >= 2 || error("Define at least two stages.")
    code = @q begin
        $scenariodef
        StochasticModel($(esc.(paramdefs)...)) do $(esc(:sp))
            $(esc(def))
        end
    end
    return prettify(code)
end

# Printing #
# ========================== #
function Base.show(io::IO, stochasticmodel::StochasticModel)
    println(io, "Multi-stage Stochastic Model")
end
function Base.show(io::IO, stochasticmodel::StochasticModel{2})
    modelstr = "minimize fâ‚€(x) + ğ”¼[f(x,Î¾)]
  xâˆˆğ’³

where

f(x,Î¾) = min  f(y; x, Î¾)
              y âˆˆ ğ’´ (x, Î¾)"
    print(io, "Two-Stage Stochastic Model\n\n")
    println(io, modelstr)
end
# ========================== #

# Stochastic data

Decoupling data design and model design is a fundamental principle in StochasticPrograms. This decoupling is achieved through data injection. By data we mean parameters in an optimization problem. In StochasticPrograms, this data is either deterministic and related to a specific stage, or uncertain and related to a specific scenario.

## Stage data

Stage data is related to parameters that always appear in the first or second stage of a stochastic program. These parameters are deterministic and are the same across all scenarios. Stage data must be supplied when creating a stochastic program, through specialized constructors. However, the data can later be mutated without having to construct a new stochastic program instance. Any stage related data can then be accessed through the reserved keyword `stage` in [`@first_stage`](@ref) and [`@second_stage`](@ref) blocks. To showcase, we consider a minimal stochastic program:
```math
\DeclareMathOperator*{\maximize}{maximize}
\begin{aligned}
 \maximize_{x \in \mathbb{R}} & \quad x + \operatorname{\mathbb{E}}_{\omega} \left[Q(x, \xi(\omega))\right] \\
 \text{s.t.} & \quad l_1 \leq x \leq u_1
\end{aligned}
```
where
```math
\begin{aligned}
 Q(x, \xi(\omega)) = \max_{y \in \mathbb{R}} & \quad q_{\omega} y \\
 \text{s.t.} & \quad y + x \leq U \\
 & \quad l_2 \leq y \leq u_2
\end{aligned}
```
and the stochastic variable
```math
  \xi(\omega) = q_{\omega}
```
takes on the value ``1`` or ``-1`` with equal probability. Here, the first stage contains the two parameters: ``l_1`` and ``u_1``. The second stage contains the three scenario-independent parameters: ``U``, ``l_2``, and ``u_2``. The following defines this problem in StochasticPrograms, with some chosen parameter values:
```@example stage
using StochasticPrograms
using GLPKMathProgInterface

@scenario Simple = begin
    q::Float64
end

ξ₁ = SimpleScenario(1., probability = 0.5)
ξ₂ = SimpleScenario(-1., probability = 0.5)

l₁ = -1.
u₁ = 1.

U = 2.
l₂ = -1.
u₂ = 1.

sp = StochasticProgram((l₁,u₁), (U,l₂,u₂), [ξ₁,ξ₂])

@first_stage sp = begin
    l₁, u₁ = stage
    @variable(model, l₁ <= x <= u₁)
    @objective(model, Max, x)
end

@second_stage sp = begin
    @decision x
    U, l₂, u₂ = stage
    ξ = scenario
    @variable(model, l₂ <= y <= u₂)
    @objective(model, Max, ξ.q*y)
    @constraint(model, y + x <= U)
end

print(sp)

print("VRP = $(VRP(sp, solver = GLPKSolverLP()))")
```
Now, we can investigate the impact of the stage parameters by changing them slightly and regenerate the problem:
```@example stage
l₁ = -2.
u₁ = 2.

U = 2.
l₂ = -0.5
u₂ = 0.5

set_first_stage_data!(sp, (l₁,u₁))
set_second_stage_data!(sp, (U,l₂,u₂))

generate!(sp) # Regenerate problem

print(sp)

print("VRP = $(VRP(sp, solver = GLPKSolverLP()))")
```

## Scenario data

Any uncertain parameter in the second stage of a stochastic program should be included in some predefined [`AbstractScenario`](@ref) type. Hence, all uncertain parameters in a stochastic program must be identified before defining the models. In brief, StochasticPrograms demands two functions from this abstraction. The discrete probability of a given [`AbstractScenario`](@ref) occurring should be returned from [`probability`](@ref). Also, the expected scenario out of a collection of given [`AbstractScenario`](@ref)s should be returned by [`expected`](@ref). StochasticPrograms provides a convenience macro, [`@scenario`](@ref), for creating scenario types that adhere to this abstraction. If the identified uncertain parameters is a collection of numerical values, this is the recommended way to define the required scenario type.

```@example
using StochasticPrograms

@scenario Example = begin
    X::Float64
end

s₁ = ExampleScenario(1., probability = 0.5)
s₂ = ExampleScenario(5., probability = 0.5)

println("Probability of s₁: $(probability(s₁))")

s = expected([s₁, s₂])

println("Expectation over s₁ and s₂: $s")
println("Expectated X: $(s.scenario.X)")

```
Here, all the required operations are correctly defined.

There are some caveats to note. First, the autogenerated requires an additive zero element of the introduced scenario type. For simple numeric types this is autogenerated as well. However, say that we want to extend the above scenario with some vector parameter of size 2:
```@example
using StochasticPrograms

@scenario Example = begin
    X::Float64
    Y::Vector{Float64}
end
```
In this case, we must provide an implementation of `zero` using [`@zero`](@ref):
```@example
using StochasticPrograms

@scenario Example = begin
    X::Float64
    Y::Vector{Float64}

    @zero begin
        return Example(0.0, [0.0, 0.0])
    end
end

s₁ = ExampleScenario(1., ones(2), probability = 0.5)
s₂ = ExampleScenario(5., -ones(2), probability = 0.5)

println("Probability of s₁: $(probability(s₁))")

s = expected([s₁, s₂])

println("Expectation over s₁ and s₂: $s")
println("Expectated X: $(s.scenario.X)")
println("Expectated Y: $(s.scenario.Y)")
```
Another caveat is that the [`expected`](@ref) function can only be auto generated for fields that support addition and scalar multiplication with `Float64`. Consider:
```@example
using StochasticPrograms

@scenario Example = begin
    X::Float64
    Y::Vector{Float64}
    Z::Int

    @zero begin
        return Example(0.0, [0.0, 0.0], 0)
    end
end
```
Again, the solution is to provide an implementation of `expected`, this time using [`@expectation`](@ref):
```@example
using StochasticPrograms

@scenario Example = begin
    X::Float64
    Y::Vector{Float64}
    Z::Int

    @zero begin
        return Example(0.0, [0.0, 0.0], 0)
    end

    @expectation begin
        X = sum([probability(s)*s.X for s in scenarios])
        Y = sum([probability(s)*s.Y for s in scenarios])
        Z = sum([round(Int, probability(s)*s.Z) for s in scenarios])
        return Example(X, Y, Z)
    end
end

s₁ = ExampleScenario(1., ones(2), 1, probability = 0.5)
s₂ = ExampleScenario(5., -ones(2), -1, probability = 0.5)

println("Probability of s₁: $(probability(s₁))")

s = expected([s₁, s₂])

println("Expectation over s₁ and s₂: $s")
println("Expectated X: $(s.scenario.X)")
println("Expectated Y: $(s.scenario.Y)")
println("Expectated Z: $(s.scenario.Z)")
```
For most problems, [`@scenario`](@ref) will probably be adequate. Otherwise consider defining [Custom scenarios](@ref).

## Sampling

```@setup sampling
using Random
Random.seed!(1)
```
Typically, we do not have exact knowledge of all possible future scenarios. However, we often have access to some model of the uncertainty. For example, scenarios could originate from:

 - A stochastic variable with known distribution
 - A time series fitted to data
 - A nerual network prediction

Even if the exact scenario distribution is unknown, or not all possible scenarios are available, we can still formulate a stochastic program that approximates the model we wish to formulate. This is achieved through a technique called *sampled average approximation*, which is based on sampling. The idea is to sample a large number ``n`` of scenarios with equal probability ``\frac{1}{n}`` and then use them to generate and solve a stochastic program. By the law of large numbers, the result will converge with probability ``1`` to the "true" solution with increasing ``n``.

StochasticPrograms accepts [`AbstractSampler`](@ref) objects in place of [`AbstractScenario`](@ref). However, an [`AbstractSampler`](@ref) is always linked to some underlying [`AbstractScenario`](@ref) type, which is reflected in the resulting stochastic program as well. Samplers are conviniently created using [`@sampler`](@ref). We can define a simple scenario type and a simple sampler as follows:

```@example sampling
using StochasticPrograms

@scenario Example = begin
    ξ::Float64
end

@sampler Example = begin
    w::Float64

    Example(w::AbstractFloat) = new(w)

    @sample begin
        w = sampler.w
        return ExampleScenario(w*randn(), probability = rand())
    end
end
```
This creates a new [`AbstractSampler`](@ref) type called `ExampleSampler`, which samples `ExampleScenario`s. Now, we can create a sampler object and sample a scenario
```@example sampling
sampler = ExampleSampler(2.)

s = sampler()

println(s)
println("ξ: $(s.ξ)")
```
It is possible to create other sampler objects for the `ExampleScenario`, by providing a new unique name:
```@example sampling
@sampler Another Example = begin
    w::Float64
    d::Float64

    Another(w::AbstractFloat, d::AbstractFloat) = new(w, d)

    @sample begin
        w = sampler.w
        d = sampler.d
        return ExampleScenario(w*randn() + d, probability = rand())
    end
end

another = AnotherSampler(2., 6.)

s = another()

println(s)
println("ξ: $(s.ξ)")
```
Now, lets create a stochastic program based on the `ExampleScenario` type:
```@example sampling
sp = StochasticProgram(ExampleScenario)

@first_stage sp = begin
    @variable(model, x >= 0)
    @objective(model, Min, x)
end

@second_stage sp = begin
    @decision x
    ξ = scenario.ξ
    @variable(model, y)
    @objective(model, Min, y)
    @constraint(model, y + x == ξ)
end
```
Now, we can sample ``5`` scenarios using the first sampler to generate ``5`` subproblems:
```@example sampling
sample!(sp, sampler, 5)
```
Printing yields:
```@example sampling
print(sp)
```
Sampled stochastic programs are solved as usual:
```@example sampling
using GLPKMathProgInterface

optimize!(sp, solver = GLPKSolverLP())

println("optimal decision: $(optimal_decision(sp))")
println("optimal value: $(optimal_value(sp))")
```
Again, if the functionality offered by [`@sampler`](@ref) is not adequate, consider [Custom scenarios](@ref).

## SSA

The command [`SSA`](@ref) is used to create sampled average approximations of a given stochastic program by supplying a sampler object.
```@example sampling
ssa = SSA(sp, sampler, 10)
```

## Custom scenarios

```@setup custom
using Random
Random.seed!(1)
```

More complex scenario designs are probably not implementable using [`@scenario`](@ref). However, it is still possible to create a custom scenario type as long as:

 - The type is a subtype of [`AbstractScenario`](@ref)
 - The type implements [`probability`](@ref)
 - The type implements [`expected`](@ref), which should return an additive zero element if given an empty array

The restriction on `expected` is there to support taking expectations in a distributed environment. We are also free to define custom sampler objects, as long as:

 - The sampler type is a subtype of [`AbstractSampler`](@ref)
 - The sampler type implements a functor call that performs the sampling

See the [Continuous scenario distribution](@ref) for an example of custom scenario/sampler implementations.
